private["_inventory","_wpns","_gearItems","_isOK","_x","_Uniform","_Vest","_WpnsItems","_Items", "_add", "_Headgear","_val","_item"];

_inventory = _this select 0;
_backpack = _this select 1;

if ((_backpack select 0) != "") then {
	player addBackpack (_backpack select 0);
};

if (count _inventory > 0) then {
	_wpns = _inventory select 0;
	_gearItems = _inventory select 1;
	_Uniform = _inventory select 2;
	_Vest = _inventory select 3;
	_WpnsItems = _inventory select 4;
	_Items = _inventory select 5;
	_Headgear = _inventory select 6;
	_Goggles = _inventory select 7;
	
	//Add uniform
	if (_Uniform != "") then {
		if (uniform player != _Uniform) then {
			player addUniform _Uniform;
		};
	};
	
	//Add vest
	if (_Vest != "") then {
		if (vest player != _Vest) then {
			player addVest _Vest;
		};
	};
	
	//Add items - Часы, компас, карта, gps ... - размещаемые в соответствующие слоты
	if (count _Items > 0) then {
		if (vest player == "") then {
			player addVest "V_PlateCarrier1_rgr";
			{
				if ((_x == "Binocular") or (_x == "Rangefinder")) then {
					player addWeapon _x;
				}else{
					player addItem _x;
					player assignItem _x;
				};
			} foreach _Items;
			removeVest player;
		} else {
			{
				if ((_x == "Binocular") or (_x == "Rangefinder")) then {
					player addWeapon _x;
				}else{
					player addItem _x;
					player assignItem _x;
				};
			} foreach _Items;
		};
	};
	
	if (_Headgear != "") then {
		player addHeadgear _Headgear;
	};
	
	if (_Goggles != "") then {
		player addGoggles _Goggles;
	};

	//Add inventory - Магазины и Итемы
	{	
		if (typeName _x == "ARRAY") then {
			_item = _x select 0;
			_val = 	_x select 1;
		} else {
			_item = _x;
			_val = -1;
		};
		
		_isOK = isClass(configFile >> "CfgMagazines" >> _item);
		if (_isOK) then {
			if (_val != -1) then {
				player addMagazine [_item,_val];
			} else {
				player addMagazine _item;
			};			
		} else {
			player addItem _item;
		};
	} foreach _gearItems;

	//Add weapons
	{
		if (_x != "") then {	
			_isOK = isClass(configFile >> "CfgWeapons" >> _x);
			if (_isOK) then {
				player addWeapon _x;
			};
		};
	} forEach _wpns;
	
	//Add weapon items
	if (count (_WpnsItems select 0) > 0) then {
		{
			if (_x != "") then {	
				player addPrimaryWeaponItem _x;
			};
		} foreach (_WpnsItems select 0);
	};
	if (count (_WpnsItems select 1) > 0) then {
		{
			if (_x != "") then {
				player addSecondaryWeaponItem _x;
			};
		} foreach (_WpnsItems select 1);
	};
	if (count (_WpnsItems select 2) > 0) then {
		{
			if (_x != "") then {
				player addHandgunItem _x;
			};
		} foreach (_WpnsItems select 2);
	};

};